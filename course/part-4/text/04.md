# 4.04 - Delegates

Delegates act as templates for instantiating the visual items inside the view. The data roles provided by the model are bound to visual item properties, such as the `Text.text` or `Image.source` property. Delegates can also be used to modify and update the data bound to the roles by simply assigning new values to roles. The model will notify all views about the changed data values. 

In the following example, we have a `TeamDelegate` that updates the bound data when clicked:

```qml
ListModel {
    id: teamModel
    ListElement {
        teamName: "Team C++"
        teamColor: "blue"
    }
}

ListView {
    anchors.fill: parent
    model: teamModel
    delegate: TeamDelegate {}
}

// TeamDelegate.qml
Component {
    Text {
        text: teamName
        color: teamColor

        MouseArea {
            anchors.fill: parent
            onClicked: {
                    model.teamName = "Team Quick"
                    model.teamColor = "red"
                }
            }
        }
    }
}

```

Within the delegate, it is possible to access the [data roles](http://doc.qt.io/qt-5/qtquick-modelviewsdata-modelview.html#qml-data-models) as internal properties. Remember also the `model` qualifier, if the delegate item property names and model roles have a name clash. 

### Delegate size

In most examples so far, we have used `Text` or `Image` QML types for delegates. These types have implicit size, so there has not been any need to define the size explicitly using the `Item::width` and `Item::height` properties. In fact, using the implicit size and setting the explicit size in `Text` incurs a performance penalty as the text must be laid out twice.

In real application, delegates seldom contain just a text or an image. More often delegates are composed from a few other QML types. As delegates are components, they must contain exactly one root item. The `Item` QML type is a good candidate for the root item, as it's suitable for composing delegates from other items. However, `Item` has no explicit size, so it has to be explicitly declared. 

One approach is to bind the root item size to the view size. In the following example, we bind the delegate height to the height available for one item, assuming the model has `n` items. To keep the text readable, we have used `FontMetrics` to calculate the minimum height for the delegate. 

```qml
ListView {
    anchors.fill: parent
    anchors.margins: 20
    clip: true
    model: 50
    delegate: numberDelegate
}

FontMetrics {
    id: fontMetrics
    font { family: "Arial"; pixelSize: 14 }
}

Component {
    id: numberDelegate
    Item {
        width: ListView.view.width
        height: Math.max(ListView.view.height / ListView.view.count, fontMetrics.height)

        Rectangle {
            anchors.fill: parent
            border.color: "black"
            Text {
                font { family: "Arial"; pixelSize: 14}
                text: 'Item ' + index;
                anchors.centerIn: parent;
            }
        }
    }
}
```

Another approach is to let the delegate children determine its size. This is useful, if children have implicit size like the `Text` item in the following example. 

```qml
Component {
    id: numberDelegate
    Item {
        id: rootItem
        width: ListView.view.width
        height: childrenRect.height

        Rectangle {
            width: rootItem.width
            height: childrenRect.height + 2
            border.color: "black"
            Text {
                font { family: "Arial"; pixelSize: 14}
                text: 'Item ' + index;
            }
        }
    }
}
```

Keep your delegates as simple as possible. This applies to size calculation as well. Avoid complex size calculations and dependencies between delegate objects.

### Exercise - PictureFrame with compound delegate

Let's change the PictureFrame exercise from the previous chapter to have little bit more complicated delegate. This will not be the best way to implement the delegate, but it's used for learning purposes. The goal is the understand, how delegate size should be determined.

* Let's modify the path view delegate. The delegate root should be an `Item` QML type. 

* The item should contain one child, which is `Column`, which is shown immediately below the border image. 

* The column contains the border image and a rectangle, which has a label as a child. The rectangle should use the image width and label height. The label is entered in the rectangle.

* The only hard-coded size can be the border image. It's totally acceptable to make the border image scalable as well, but it's not required in the exercise. 

* All other delegate objects must have either implicit size or the `width` and `height` properties bound to other properties. No hard-coded magic numbers are accepted. 

## Clip

The view `clip` property will ensure that any view items outside of the view will not be visible. If set to `false`, items will 'flow over' the view. Avoid using `clip` in delegates. If `clip` is enabled inside a delegate, each delegate will be batched separately. The QML renderer creates a scene graph of all the visible items. It tries to minimise the number of OpenGL state changes, by batching paint operations. One batch contains only operations, which do not require an OpenGL state changes. Less batches result to better rendering performance. 

Clipping results that a scene graph node and its complete sub-tree will be put into one batch. If `clip` is used in the view, but not in the delegates, the whole tree of the view and delegates can be put into one batch. However, as soon as a delegate uses `clip`, a new scene graph sub-tree is created for each delegate. this will increase the number of batches and have negative impact on the performance.

[`Scenegraph renderer`](http://doc.qt.io/qt-5/qtquick-visualcanvas-scenegraph-renderer.html) describes batching in more detail. 

### Exercise - batch optimisation

This exercise does not use the TMC plugin. The goal is to get familiar with the utilities to find and optimise batches in QML application.

* Define an environment variable `QSG_RENDERER_DEBUG=render` and run your PictureFrame application in Qt Creator in Debug Mode. You will see the number of batches sent to OpenGL in Debug Console. 

* Use the `QSG_VISUALIZE` environment variable to visualise the batches. The variable may have one of the four values: `batches`, `clip`, `overdraw` or `changes`. Set the `clip` property to `true` in your view. Check, if this affects the number of batches in any way.

## Memory management

Dynamic views create and destroy delegates dynamically. The only exception is `TableView`, which is able to re-use existing delegates from the pool as well. `TableView` has the `reuseItems` property to control this. 

`ListView`, `GridView`, and `PathView` will create as many delegate items, as the view is able to show in its area. This allows Qt developers to use huge item models, consisting of millions of items, as only a small portion of items is visible and created at one time. When a user flicks the view, visible items go beyond the view are and they are destroyed. New items will be created and they become visible in the view. 

Dynamic creation of items implies you must never store state information into a delegate. Always store the state information into the model, before the item is destroyed.

```qml
Component.onDestruction: {
    someBooleanRole = (state === "false") ? false : true;
}
```

Views provide caches to improve the performance, when the user is flicking the view. `ListView` and `GridView` use `cacheBuffer`, while `PathView` uses `cacheItemCount`. `PathView::pathItemCount` determines how many visible items are created and `PathView::cacheItemCount` determines how many additional items are created in the cache. Caching delegate items improves the performance, but increases the memory consumption.  

The property `cacheBuffer` is an integer, determining how many delegate items are cached. If the value is 100 in a list view and the delegate height is 20, 5 items will be cached before and 5 items will be cached after the currently visible items. 

In `GridView`, the caching principle is similar. If in a vertical view the delegate is 20 pixels high, there are 3 columns and `cacheBuffer` is set to 40, then up to 6 delegate items above and 6 delegate items below the visible area may be created or retained. 

`TableView` can also reuse delegate items. When an item is flicked out, it moves to the reuse pool, which is an internal cache of unused items. When this happens, the `TableView::pooled` signal is emitted to inform the item about it. Likewise, when the item is moved back from the pool, the `TableView::reused` signal is emitted. 

Any item properties that come from the model are updated when the item is reused. This includes index, row, and column, but also any model roles.

Avoid storing any state inside a delegate. If you do, reset it manually on receiving the `TableView::reused` signal.

If an item has timers or animations, consider pausing them on receiving the `TableView::pooled` signal. That way you avoid using the CPU resources for items that are not visible. Likewise, if an item has resources that cannot be reused, they could be freed up.

The following example shows a delegate that animates a spinning rectangle. When it is pooled, the animation is temporarily paused.

```qml
Component {
    id: tableViewDelegate
    Rectangle {
        implicitWidth: 100
        implicitHeight: 50

        TableView.onPooled: rotationAnimation.pause()
        TableView.onReused: rotationAnimation.resume()

        Rectangle {
            id: rect
            anchors.centerIn: parent
            width: 40
            height: 5
            color: "green"

            RotationAnimation {
                id: rotationAnimation
                target: rect
                duration: (Math.random() * 2000) + 200
                from: 0
                to: 359
                running: true
                loops: Animation.Infinite
            }
        }
    }
}
```

### Exercise - Grid view vs. table view

Let's compare, how grid view and table view manage the delegate lifetime. 

* Create a new Qt Quick Control application from the scratch. 

* Split the window horizontally between a `GridView` and `TableView`. Use Qt Quick `TableView`, introduced in Qt 5.12.

* Create an empty `ListModel`. Fill the list model with 100 element. Each element has two roles: "name" and "buttonChecked". The name value could be just an index and buttonChecked should be initially false. Assign to model to both views' `model` property. You may find `Component.onCompleted` signal handler very useful. After the window is created, use the handler to fill the model. 

* Create two delegates: one for the grid view and another one for the table view. You can use similar delegate declaration in both cases. Add a `Row` of a `RadioButton` and `Text`. Set `RadioButton::autoExclusive` to false in both delegates. Otherwise. we cannot uncheck the checked radio button. The `Text::text` property should have the "name" role, defined in the model. `ButtonChecked` role is not used yet.

* Add `Component::onCompleted` signal handler to both delegates and use `console.log` to notify about the delegate creation. Flick the grid view and table view up and down. What do you observe? How delegate creation differs between the views?

* Check one radio button in the grid and one in the table. Scroll down the both views? What do you observe? 

* We need to store the radio button state into the model. That's why we have the `buttonChecked` property. First, bind `RadioButton::checked` to `buttonChecked`. Now, you should see that checking a radio button in one view checks the radio button in other view, as they share the same model. 

* Finally, save the radio button state changes to the model. If you now scroll down and up, you should observe that the radio button state does not change or the table view does not have any re-used radio buttons in the wrong state. 


## Complex delegates

Keep your delegates as simple as possible. There are ways to manage complex delegates, but almost always the best solution performance and memory consumption wise is to declare simple delegates. 

Why should delegates be so simple then? Problems arise when delegates contain, for example [`Video`](https://doc.qt.io/qt-5/qml-qtmultimedia-video.html) items all auto-playing video for some odd reason.

```qml
Item {
    id: videoDelegate

    Video {
        id: video
        width: 800
        height: 600
        source: model.videosrc
        autoPlay: true
    }
}
```

The target platform is low-end devices, and loading all these items at startup will take an noticeable amount of time. Some devices might not be able to play multiple video sources at the same time.

If there is a need to load resource intensive items inside a delegate, it is possible to create objects dynamically, as explained in [Dynamic object creation](https://doc.qt.io/qt-5/qtqml-javascript-dynamicobjectcreation.html).

QML has the [Loader](https://doc.qt.io/qt-5/qml-qtquick-loader.html) type, which can be used to load different parts of the UI, like complicated parts of delegates, to improve performance. The following example has a delegate which has a `Component` where the video will be contained. We add a `Loader` and a `MouseArea` to the delegate, so when clicked the `sourceComponent` property will be set to the `videoComponent`. This change will trigger the `Loader` to load the `videoComponent` component.

```qml
Item {
    id: lazyVideoDelegate

    width: 200
    height: 200

    Component {
        id: videoComponent

        Video {
            id: video
            width: 800
            height: 600
            source: model.videosrc
            autoPlay: true
        }
    }

    Loader {
        id: videoLoader
    }

    MouseArea {
        anchors.fill: parent
        onClicked: videoLoader.sourceComponent = videoComponent
    }
}
```

Notice that while a `Video` item declared by itself would be automatically rendered and displayed, this is not the case for the above as the item is defined inside a Component.

This will reduce the load time considerably, as the delegate will only contain `Loader` and `MouseArea` objects initially. Note however that we have to create a `Loader` object, which is not free. It is lighter, but consumes memory anyway. The better way is to try to avoid `Loaders` in delegates. The best approach would be to add an `Image` which displays a thumbnail of the video or something similar.

### Exercise - Memory intensive app

In this part, we have concentrated on dynamic views. What are the static views then? Positioners are static views. The [Repeater](http://doc.qt.io/qt-5/qml-qtquick-repeater.html) QML type is often used to statically create content for positioners. The idea is similar to the dynamic views. `Repeater` uses delegate templates to create objects, but all objects are created synchronously. 

* You are given the `MemoryIntensiveElement` QML file. Create an empty QML application with the Qt Creator wizard and add the given QML file to the project.

* Use `MemoryIntensiveElement::nofItems` property to create e.g. 150,000 delegates. The time required for the creation depends on your computer resources, so adjusted the number so that the startup time is at least several seconds.

* Your task is simple. Reduce the startup time with asynchronous loading. You MUST not change anything in `MemoryIntensiveElement`. 

***

### Exhaustive reference material mentioned in this topic

http://doc.qt.io/qt-5/model-view-programming.html  
https://qmlbook.github.io/en/ch06/index.html#delegate  
https://www.quora.com/What-are-delegates-in-Qt  
https://doc.qt.io/archives/qq/qq24-delegates.html  
http://doc.qt.io/qt-5/qitemdelegate.html  
https://www.ics.com/designpatterns/book/delegates.html  
http://doc.qt.io/qt-5/qitemdelegate.html#details  


