# 4.03 Views 

For dynamic views QML provides four commonly used types, `ListView`, `GridView`, `TableView`, and `PathView. First three inherit `Flickable` type, which enables users to flick content either horizontally or vertically. Note that there is a `TableView` type in Qt Quick Controls, but those types are deprecated and not covered here. `PathView` allows organising data items in any path shape. 

Each view may take any of the previously described QML model type and organise and decorate delegate items in the UI. All QML models are lists. `TableView` is the only type, which can directly show several columns from a C++ table model, i.e. `QAbstractItemModel` subclass. This is not any limitation though, as other views may use delegates, which display model columns, mapped to different roles. 

In this chapter, we concentrate on use cases, where the view uses a QML model. C++ models are covered in Phase 5. 

## ListView

[`ListView`](http://doc.qt.io/qt-5/qml-qtquick-listview.html) organises delegates in a horizontal or vertical list. `ListView` provides a variety of ways to decorate lists. Data items may be grouped into sections for better visualisation. The list may have a special header and footer item, and a highlight, indicating the current item.  

In the following example, the `clip` property is set to `true`. In QML, parent items do not clip their children, so children may paint outside the bounding box of a parent. In most cases, this is ok. However, if there is, e.g. a tool bar and list view in the same UI window, quite likely we do not want the list view items to be painted on the top of the tool bar. Settings `clip` to `true` clips children, which paint outside the list view area. 

```qml
ListView {
    anchors.fill: parent
    clip: true
    model: 50
    delegate: someNiceDelegate
}

```

### Attached properties 

Each delegate gets an access to a number of attached properties, provided by the view. For example, `ListView` to which the delegate is bound is accessible from the delegate through the `ListView.view` property. Or a delegate can check, whether it is the current item by using `ListView.isCurrentItem`.

Attach properties allow implementing clean, re-usable delegates, which do not have direct bindings to the view via an `id`, for example. 

The attached properties are available to the root delegate item. Child objects must refer to attached properties using the root delegate identifier as in the following example. Sometimes a new custom var property, bound to an attached property, id declared. Remember however that each additional property increases the memory consumption.

```qml
ListView {
    width: 300; height: 200
    model: contactsModel
    delegate: contactsDelegate

    Component {
        id: contactsDelegate
        Rectangle {
            id: rootWrapper
            width: 80
            height: 80
            // Rectangle is the root item, we can refer to the attached property directly
            color: ListView.isCurrentItem ? "black" : "red"
            Text {
                id: contactInfo
                text: name + ": " + number
                // Attached property is not accessible in the child
                color: rootWrapper.ListView.isCurrentItem ? "red" : "black"
            }
        }
    }

}
```

### Sections 

Grouping data items into sections improve readability in a list view. For example, albums grouped by artist names, or employees grouped by the department would easily show, which items are related.

When using sections, two properties need to be considered. In `ListView` the property `section.property` defines which property is used to divide the data into sections. It's important to note that the model *needs to be sorted* so that each property forming a section is continuous. If a property forming the section appears in multiple non-continuos places in the model, the same section might appear multiple times in the view. The second property to be considered is `section.criteria`. It's set to `ViewSection.FullString` by default, which means that the whole property is used as the section. If set to `ViewSection.FirstCharacter` only the first character in the property is used for the section, for example when dividing up a list of names to sections based on the first letter of the last name.

After the sections have been defined, they can be accessed from the items using the attached properties `ListView.section`, `ListView.previousSection`, and `ListView.nextSection`.

We can also assign a special section delegate to create a header before each section, by assigning it to the property `section.delegate` in the `ListView`.

In the following example, the data model has a list of artist and their albums, which are divided into sections by artist.

```qml
ListView {
    anchors.fill: parent
    anchors.margins: 20
    clip: true
    model: albumsAndArtists
    delegate: albumDelegate
    section.property: "artist"
    section.delegate: sectionDelegate
}

Component {
    id: albumDelegate
    Item {
        width: ListView.view.width
        height: 20
        Text {
            anchors.left: parent.left
            anchors.verticalCenter: parent.verticalCenter
            anchors.leftMargin: 10
            font.pixelSize: 12
            text: album
        }
    }
}

Component {
    id: sectionDelegate
    Rectangle {
        width: ListView.view.width
        height: 20
        color: "lightblue"
        Text {
            anchors.left: parent.left
            anchors.verticalCenter: parent.verticalCenter
            anchors.leftMargin: 6
            font.pixelSize: 14
            text: section
        }
    }
}

ListModel {
    id: albumsAndArtists
    ListElement { album: "Crazy World"; artist: "Scorpions"; }
    ListElement { album: "Love at First Sting"; artist: "Scorpions"; }
    ListElement { album: "Agents of Fortune"; artist: "Blue Öyster Cult"; }
    ListElement { album: "Spectres"; artist: "Blue Öyster Cult"; }
    ListElement { album: "The Vale of Shadows"; artist: "Red Raven Down"; }
    ListElement { album: "Definitely Maybe"; artist: "Oasis"; }
}
```

### Exercise - PictureFrame with sections (4/8)

This time continue the PictureFrame application by adding sections to it.

* Organise images into groups using `ListView` sections. 

* There is no need to group images in the model, so there may be any number of .tiff sections in the list view. 

* Change the worker script as well to support sections. 

### Headers and Footers

Views allow visual customisation through decoration properties such as the `header` and `footer`. By binding an object, usually another visual object, to these properties, the views are decoraable. As an example, a footer may include a `Rectangle` type showcasing borders, or a header that displays a logo on top of the list. It should be noted that headers and footers don't respect the `spacing` property in `ListView`, and thus any spacing needs to be a part of the header/footer itself.

```qml
ListView {
    anchors.fill: parent
    anchors.margins: 20
    clip: true
    model: 4
    delegate: numberDelegate
    spacing: 2
    header: headerComponent
    footer: footerComponent
}

Component {
    id: headerComponent
    Rectangle {
        width: ListView.view.width
        height: 20
        color: "lightBlue"
        Text { text: 'Header'; anchors.centerIn: parent; }
    }
}

Component {
    id: footerComponent
    Rectangle {
        width: ListView.view.width
        height: 20
        color: "lightGreen"
        Text { text: 'Footer'; anchors.centerIn: parent; }
    }
}

Component {
    id: numberDelegate
    Rectangle {
        width: ListView.view.width
        height: 40
        border.color: "black"
        Text { text: 'Item ' + index; anchors.centerIn: parent; }
    }
}
```

### Exercise - PictureFrame with a footer (5/8)

Add a footer to the PictureFrame application. 

* The footer should be always visible, if there are any items in the model. It should not go out of the view, when the list is scrolled. 

* The footer must not be overpainted by the view and it must show the url of the current image in a `Label` Qt Quick Control. 

### Keyboard navigation and highlighting

When using a keyboard to navigate in the ListView, some form of highlighting is necessary to tell which item is currently selected. Two things are necessary to allow keyboard navigation in the ListView. First, the view needs to be given keyboard focus with the property `focus: true`, and second a special highlight delegate needs to be defined. This is demonstrated in the following example:

```qml
ListView {
    id: view
    anchors.fill: parent
    anchors.margins: 20
    clip: true
    model: 20
    delegate: numberDelegate
    spacing: 5
    highlight: highlightComponent
    focus: true
}

Component {
    id: highlightComponent
    Rectangle {
        color: "lightblue"
        radius: 10
    }
}

Component {
    id: numberDelegate
    Item {
        width: ListView.view.width
        height: 40
        Text {
            anchors.centerIn: parent
            font.pixelSize: 10
            text: index
        }
    }
}
```
The highlight delegate is given the `x`, `y` and `height` of the current item. If the `width` is not specified, the width of the current item is used.

### Exercise - PictureFrame with the highlight (6/8)

Add a highlight into the PictureFrame application.

* The highlight should be a red ellipse, shown in the middle, right border of the image. You may add small margin that the ellipse does not overlap the border.

* The highlight size may be hard-coded, e.g. to 10 x 10 pixels. 

* Animate the highlight position change using an out bounce easing curve. 

## GridView and TableView

[`GridView`](http://doc.qt.io/qt-5/qml-qtquick-gridview.html) is largely similar to `ListView`, and it's used in almost the same way. The main difference is, that it doesn't rely on spacing and size of delegates, and instead `cellWidth` and `cellHeight` are defined in the view. Header, footers, keyboard navigation with highlighting are all available in `GridView`. Orientation is set in the `flow` property, options being `GridView.LeftToRight` (default) and `GridView.TopToBottom`. The following example showcases the usage of `GridView`.

```qml
GridView {
    id: grid
    anchors.fill: parent
    cellWidth: 80;
    cellHeight: 80
    model: 100
    delegate: numberDelegate
    highlight: Rectangle { color: "lightsteelblue"; radius: 5 }
    focus: true
}

Component {
    id: numberDelegate
    Item {
        width: grid.cellWidth
        height: grid.cellHeight
        Text {
            text: index
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.verticalCenter: parent.verticalCenter
        }
    }
}
```

[`TableView`](http://doc-snapshots.qt.io/qt5-5.12/qml-qtquick-tableview.html#) can display multiple columns, provided that the underlying model has multiple columns. There is no QML model type with multiple columns, so the model must inherit `QAbstractItemModel`. 

The `GridView` and `TableView` exercise is postponed to Phase 4, where we compare how the views manage the lifetime of delegates. 

## PathView

[PathView](http://doc.qt.io/qt-5/qml-qtquick-pathview.html) is the most powerful and customisable of the dynamic views provided by QML. `PathView` will display the model data on a [Path](http://doc.qt.io/qt-5/qml-qtquick-path.html) that can be arbitrarily defined with the `Path` QML type. `PathView` can be customised through a wide variety of properties, such as `pathItemCount`, which controls the numbers of visible items at once, and `preferredHighlightBegin` and `preferredHighlightEnd`, which are used to control where along the path the current item is shown. The properties expect real values between 0 and 1. Setting both of them to, for instance, 0.5 would display the current item at the location 50% along the path.

A `Path` is the path that the delegates follow when the view is scrolled. It defines the path using `startX` and `startY` properties, alongside path segments that are defined types such as [PathQuad](http://doc.qt.io/qt-5/qml-qtquick-pathquad.html) or [PathLine](http://doc.qt.io/qt-5/qml-qtquick-pathline.html). All the different path segments can be found in the Qt documentation. Qt Quick Designer commercial version even has an editor to create and remove path segments and define their shapes.

The following example shows items on a straight path.
```qml
PathView {
    id: view
    model: 20
    anchors.fill: parent

    path: Path {
        startX: 0
        startY: height

        PathCurve {
            x: view.width
            y: 0
        }
    }
    delegate: Text {
        text: "Index " + index
    }
}
```

When the path has been defined, we can further tune it using `PathPercent` and `PathAttribute` types. These objects can be placed in between the path segments to provide a more fine grained control over the path and the delegates. The `PathPercent` allows you to manipulate the spacing between items on a PathView's path. You can use it to bunch together items on part of the path, and spread them out on other parts of the path. The `PathAttribute` object allows attributes consisting of a name and a value to be specified for various points along the path. The attributes are exposed to the delegate as Attached Properties, and can be used to control any property. The value of an attribute at any particular point along the path is interpolated from the `PathAttributes` bounding that point.

Next we have a larger example of `PathView`, where the path is defined with `PathQuad` and the size and opacity of the items is changed with `PathAttribute`. We've also enabled keyboard navigation, which is not available by default. It can be done by giving the keyboard focus by setting the `focus` property to true, and defining what the keys should do (in this case `Keys.onLeftPressed: decrementCurrentIndex()` and `Keys.onRightPressed: incrementCurrentIndex()` to allow moving back and forth).

```qml
// ContactModel.qml
ListModel {
    ListElement { name: "Linus Torvalds"; icon: "pics/qtlogo.png"; }
    ListElement { name: "Alan Turing"; icon: "pics/qtlogo.png"; }
    ListElement { name: "Margaret Hamilton"; icon: "pics/qtlogo.png"; }
    ListElement { name: "Ada Lovelace"; icon: "pics/qtlogo.png"; }
    ListElement { name: "Tim Berners-Lee"; icon: "pics/qtlogo.png"; }
    ListElement { name: "Grace Hopper"; icon: "pics/qtlogo.png"; }
}
```

```qml
Rectangle {
    width: 250; height: 200

    Component {
        id: delegate
        Item {
            width: 80; height: 80
            scale: PathView.iconScale
            opacity: PathView.iconOpacity
            Column {
                Image { anchors.horizontalCenter: nameText.horizontalCenter; width: 64; height: 64; source: icon }
                Text { id: nameText; text: name; font.pointSize: 12 }
            }
        }
    }

    PathView {
        anchors.fill: parent
        model: ContactModel {}
        delegate: delegate
        focus: true
        Keys.onLeftPressed: decrementCurrentIndex()
        Keys.onRightPressed: incrementCurrentIndex()
        path: Path {
            startX: 120
            startY: 100
            PathAttribute { name: "iconScale"; value: 1.0 }
            PathAttribute { name: "iconOpacity"; value: 1.0 }
            PathQuad { x: 120; y: 25; controlX: 260; controlY: 75 }
            PathAttribute { name: "iconScale"; value: 0.3 }
            PathAttribute { name: "iconOpacity"; value: 0.5 }
            PathQuad { x: 120; y: 100; controlX: -20; controlY: 0 }
        }
    }
}
```

When using `Image` QML type in the `PathView` delegates, it is useful to bind the `smooth` property to `PathView.view.moving`. Less processing power is spent on smooth transformations, when the view is in motion, while images are smoothly transformed when stationary. 

### Exercise - PictureFrame with PathView (7/8)

Let's improve the photo browsing in the PictureFrame application. As images are hard-coded to be quite large, it's difficult to quickly find the right picture. Let's add a `PathView` to improve the browsing functionality.

* Split the window horizontally into to equal size areas. Keep the list view on the left and add path view to the right. 

* Implement the path view into its own file. 

* Implement a U-shape path using three path lines. The first line goes diagonally down from the top left corner. The second path line goes horizontally to the right and the third line goes diagonally to the top right corner.

* Change the delegate opacity and size to 50 per cent in diagonal lines. 


### Exhaustive reference material mentioned in this topic

https://qmlbook.github.io/en/ch06/index.html  
https://doc.qt.io/qt-5/qml-qtquick-listview.html  
https://doc.qt.io/qt-5/qml-qtquick-gridview.html  
https://doc.qt.io/qt-5/qml-qtquick-pathview.html 
https://doc.qt.io/qt-5/qml-qtquick-tableview.html  
 
