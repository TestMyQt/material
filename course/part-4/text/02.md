# 4.02 QML Models

There are several other QML model types inn addition to `ListModel`. `XmlistModel` allows construction of a model from an XML data source. It provides a very convenient way to parse XML data. ÒbjectModel` contains the visual items to be used in a view. No delegate is required as the model already contains visual items. `DelegateModel` is useful in cases, where ´QAbstractItemModel` index is used to access data items. The typical use case are hierarchical tree models, where the user needs to navigate up and down the subtree. 

It is possible to use an object instance as a model as well. In this case, object properties are provided as roles as in the following example.

```qml
Text {
    id: myText
    text: "Hello"
    visible: false // we want to show the text in the view 
}

Component {
    id: myDelegate
    Text { text: model.text } // the qualifier needed 
}

ListView {
    anchors.fill: parent
    model: myText
    delegate: myDelegate
}```

## XmlListModel

[XmlListModel](https://doc.qt.io/qt-5/qml-qtquick-xmllistmodel-xmllistmodel.html) is used to create a read-only model from XML data. It's a very convenient way to read data from RSS feeds and create a data model from relevant XML elements. 

A simple XML structure could be the following:

```xml
<?xml version="1.0" encoding="utf-8"?>
<rss version="2.0">
    <channel>
        <item>
            <title>A blog post</title>
            <pubDate>Sat, 07 Sep 2010 10:00:01 GMT</pubDate>
        </item>
        <item>
            <title>Another blog post</title>
            <pubDate>Sat, 07 Sep 2010 15:35:01 GMT</pubDate>
        </item>
    </channel>
</rss>
```
The XML document contains **tags** (`<rss>`), which can have **attributes** (`version="2.0"`). Start tag `<item>` and end tag `</item>` form an **element**, and an element can have child elements. This basically means we have a tree of nodes, and we need to traverse the tree to extract data.

The following example shows, how to create a model from XML data.

```qml
import QtQuick 2.0
import QtQuick.XmlListModel 2.0

XmlListModel {
    id: xmlModel
    source: "http://www.mysite.com/feed.xml"
    query: "/rss/channel/item"

    XmlRole { name: "title"; query: "title/string()" }
    XmlRole { name: "pubDate"; query: "pubDate/string()" }
}
```
The `source` property defines the location for the XML document, which can be either local or remote resource.

The `query` property value should contain a valid [XPath (XML Path Language)](https://en.wikipedia.org/wiki/XPath) selector. Among other things, XPath is used to select nodes from the XML document tree that match a given path expression. 

With the query of `/rss/channel/item` we select any `<item>` that are children of `<channel>` that are children of the document root element `<rss>` to be the items for the model. See [XPath usage examples](https://en.wikipedia.org/wiki/XPath#Usage_examples) for more detailed usage.

The roles are defined with [`XmlRoles`](https://doc.qt.io/qt-5/qml-qtquick-xmllistmodel-xmlrole.html). Notice that the `<item>` elements contain other arbitrary elements, so we need a query to bind specific data. In the example, we query the `<title>` and `<pubDate>` elements inside the `<item>`, get their values with the `string()` function and bind the value to the aptly named roles `title` and `pubDate`. See [`XmlRole::query`](https://doc.qt.io/qt-5/qml-qtquick-xmllistmodel-xmlrole.html#query-prop) for more examples.

Using the generated models in `ListView` works as usual. The delegate can bind to model roles.  

```qml
ListView {
    width: 180; height: 300
    model: xmlModel
    delegate: Text { text: title + ": " + pubDate }
}
```

### Exercise - News reader

Implement a news reader app. The news are available in http://feeds.bbci.co.uk/news/rss.xml. Any other feed may be used, but the feed must provide at least some text data + URLs to get further information.

* Read all `items` tags from the feed.

* Get the news `title` and `link` from the news items. 

* The delegate should show the title and link in a column. 

* When the user clicks on the delegate item, the link is opened in the Qt web engine.

* Split the screen vertically between the view and web engine. Note that to be able to use the web engine in QML, you must call `QtWebEngine::initialize()` in C++. 

## Object Model

[ObjectModel](http://doc.qt.io/qt-5/qml-qtqml-models-objectmodel.html) defines a set of items rather than non-visual list elements. This makes it unnecessary to use delegates. 

```qml
ObjectModel {
    id: itemModel
    Text { color: "red"; text: "Hello " }
    Text { color: "green"; text: "World " }
    Text { color: "blue"; text: "again" }
}

ListView {
    anchors.fill: parent
    model: itemModel
    orientation: Qt.Horizontal
}
```

## Delegate Model

[DelegateModel](https://doc.qt.io/qt-5/qml-qtquick-xmllistmodel-xmllistmodel.html) is similar to `ObjectModel` in that sense that it also encapsulates the model and delegate together. In addition, it is used in hierarchical models. Note that all QML model types are actually lists. Table, tree, and hierarchical models can be created by subclassing `QAbstractItemModel`. `DelegateModel` has a `rootIndex` property, which is a `QModelIndex` type. The model index allows navigating between child and parent items in the tree structure. This is covered in more detail in Phase 5. 

`DelegateModel` is also useful for sharing data items to delegates in multiple views or for sorting and filtering items.

### Sharing data items

`DelegateModel` uses a `Package` QML type to enable delegates with a shared context to be provided to multiple views. Any item within a `Package` may be assigned a name via the `Package.name` attached property, similarly to the following example. There are two items with different package names. The actual content `Text` is parented based on the delegate index to either a package, containing odd or to a package, containing even indices. The model `myModel` contains just list elements with a `display` role. 

```qml
DelegateModel {
    id: visualModel
    delegate: Package {
        Item { id: odd; height: childrenRect.height; Package.name: "oddIndex" }
        Item { id: even; height: childrenRect.height; Package.name: "evenIndex" }
        Text {
            parent: (index % 2) ? even : odd
            text: display
        }
    }
    model: myModel
}
```

Now it is possible to use package names in different views. The `DelegateModel` property `parts` selects a `DelegateModel` which creates delegates from the part named. 

```qml
ListView {
    height: parent.height/2
    width: parent.width
    model: visualModel.parts.oddIndex
}

ListView {
    y: parent.height/2
    height: parent.height/2
    width: parent.width
    model: visualModel.parts.evenIndex
}
```

### Sorting and filleting data items

Delegates may be sorted and filtered using groups in `DelegateView`. Each delegate belongs to an `items` group by default. Additional groups can be defined using `DelegateModelGroup` QML type. It provides methods to create and remove items in the group or move items to a different index position inside the group. 

The first step to use groups is to add one or more groups to `DelegateModel`. In the following example, there are two groups and by default all the delegate items are added to the group `group1`. Note that items are also added to the default `items` group, so any delegate may belong to several groups. The `DelegateModel` property `filterOnGroup` determines, we are interested in delegates in `group1` only.

```qml
DelegateModel {
    id: visualModel
    model: myModel // contains ListElements with the display role 
    filterOnGroup: "group1"

    groups: [
        DelegateModelGroup { name: "group1"; includeByDefault: true },
        DelegateModelGroup { name: "group2" }
     ]
```

For every group defined in `DelegateModel` two attached properties are added to each delegate item. The first of the form `DelegateModel.inGroupName` holds whether the item belongs to the group and the second `DelegateModel.groupNameIndex` holds the index of the item in that group.

The attached properties are useful  in checking and changing the group, to which the delegate belongs to or to change the position of an item in that group. The following example shows, how the group can be changed by a mouse press.


```qml
delegate: Text {
    id: item
    text: display
    MouseArea {
        anchors.fill: parent
        onClicked: {
            if (item.DelegateModel.inGroup1) {
                item.DelegateModel.inGroup1 = false;
                item.DelegateModel.inGroup2 = true;
            }
            else {
                item.DelegateModel.inGroup1 = true;
                item.DelegateModel.inGroup2 = false;
            }
      }
   }
}
```
The group filter can be changed to display delegates in any of the groups. Only one group can be displayed at the time time. 

```qml
ListView {
    anchors.fill: parent
    model: visualModel
    focus: true
    Keys.onReturnPressed: {
        visualModel.filterOnGroup = (visualModel.filterOnGroup === "group1") ? "group2" : "group1";
    }
}
```

### Exercise - PictureFrame with views for different image types 

You have tons of pictures and your picture frame does not provide any support to organise images. Let's add support for at least viewing different image types in different views. 

* Split the list view in PictureFrame to two list views horizontally.

* Remove the delegate and create a new `DelegateModel`. 

* Split images into two categories, e.g. .jpg and other images. 

* One view should show .jpg files or whatever format you have chosen. The other view shows other images.

* Identify the image type from the file name extension. 

### Exhaustive reference material mentioned in this topic

https://doc.qt.io/qt-5/qtquick-modelviewsdata-modelview.html#xml-model
http://doc.qt.io/qt-5/qml-qtquick-xmllistmodel-xmllistmodel.html
https://qmlbook.github.io/en/ch06/index.html#a-model-from-xml
http://doc.qt.io/qt-5/xquery-introduction.html
http://doc.qt.io/qt-5/qml-qtqml-models-objectmodel.html
http://doc.qt.io/qt-5/qml-qtqml-models-delegatemodel.html



