# 3.07 - Anchors

## Anchors

In addition to the more traditional `Grid`, `Row`, and `Column`, Qt Quick also provides a way to layout items using the concept of anchors. Each item can be thought of as having a set of 7 invisible "anchor lines": left, horizontalCenter, right, top, verticalCenter, baseline, and bottom.

![QML anchors]({{ "/assets/images/part-2/QML_anchors.png" | absolute_url }})

The baseline (not pictured above) corresponds to the imaginary line on which text would sit. For items with no text it is the same as top.

The Qt Quick anchoring system allows you to define relationships between the anchor lines of different items. For example, you can write:

```qml
Rectangle { id: rect1; ... }
Rectangle { id: rect2; anchors.left: rect1.right; ... }
```

In this case, the left edge of `rect2` is bound to the right edge of `rect1`, producing the following:

![QML anchors]({{ "/assets/images/part-2/anchors_1.png" | absolute_url }})

You can specify multiple anchors. For example:

```qml
Rectangle { id: rect1; ... }
Rectangle { id: rect2; anchors.left: rect1.right; anchors.top: rect1.bottom; ... }
```

![QML anchors]({{ "/assets/images/part-2/anchors_2.png" | absolute_url }})

By specifying multiple horizontal or vertical anchors you can control the size of an item. Below, `rect2` is anchored to the right of `rect1` and the left of `rect3`. If either of the blue rectangles are moved, `rect2` will stretch and shrink as necessary:

```qml
Rectangle { id: rect1; x: 0; ... }
Rectangle { id: rect2; anchors.left: rect1.right; anchors.right: rect3.left; ... }
Rectangle { id: rect3; x: 150; ... }
```

![QML anchors]({{ "/assets/images/part-2/anchors_3.png" | absolute_url }})

There are also some convenience anchors. `anchors.fill` is a convenience that is the same as setting the left, right, top, and bottom anchors to the left,right,top and, bottom of the target item. `anchors.centerIn` is another convenience anchor, and is the same as setting the `verticalCenter` and `horizontalCenter` anchors to the `verticalCenter` and `horizontalCenter` of the target item.

For performance reasons, you can only anchor an item to its siblings and direct parent. For example, the following anchor is invalid and would produce a warning:

```qml
// bad code
Item {
    id: group1
    Rectangle { id: rect1; ... }
}
Item {
    id: group2
    Rectangle { id: rect2; anchors.left: rect1.right; ... } // invalid anchor!
}
```

Also, anchor-based layouts **cannot** be mixed with absolute positioning. If an item specifies its `x` position and also sets `anchors.left`, or anchors its left and right edges but additionally sets a width, the result is undefined, as it would not be clear whether the item should use anchoring or absolute positioning. The same can be said for setting an item's `y` and height with `anchors.top` and `anchors.bottom`, or setting `anchors.fill` as well as width or height. The same applies when using positioners such as `Row` and `Grid`, which may set the item's `x` and `y` properties. If you wish to change from using anchor-based to absolute positioning, you can clear an anchor value by setting it to undefined.

## Margins

The anchoring system also allows margins and offsets to be specified for an item's anchors. Margins specify the amount of empty space to leave to the outside of an item's anchor, while offsets allow positioning to be manipulated using the center anchor lines. An item can specify its anchor margins individually through `leftMargin`, `rightMargin`, `topMargin` and `bottomMargin`, or use `anchors.margins` to specify the same margin value for all four edges. Anchor offsets are specified using `horizontalCenterOffset`, `verticalCenterOffset` and `baselineOffset`.

![QML margins]({{ "/assets/images/part-2/QML_margins.png" | absolute_url }})

The following example specifies a left margin:

```qml
Rectangle { id: rect1; ... }
Rectangle { id: rect2; anchors.left: rect1.right; anchors.leftMargin: 5; ... }
```

In this case, a margin of 5 pixels is reserved to the left of `rect2`, producing the following:

![QML margins]({{ "/assets/images/part-2/margins_1.png" | absolute_url }})

Note: Anchor margins only apply to anchors; they are not a generic means of applying margins to an `Item`. If an anchor margin is specified for an edge but the item is not anchored to any item on that edge, the margin is not applied.

***

## Exercise - AnchoringShips

You have two nautical themed types available: Wheel and Anchor in their own files Wheel.qml and Anchor.qml

1. Add a Wheel and make it centered, horizontally and vertically in this parent 'root' container.

2. Make the Wheel half the width and half the height of the parent 'root' container.

3. Create Anchors for the Wheel and set them up like this:

```
⚓ ⚓ ⚓
⚓ ☸️ ⚓
⚓ ⚓ ⚓
```

Where `☸️` is the Wheel item and `⚓` are the Anchor items.
This means you will need a Wheel item and 8 Anchor items in total.

The Wheel should be the [visual parent](https://doc.qt.io/qt-5/qtquick-visualcanvas-visualparent.html)
of the Anchor items, any transform or rotation to the Wheel should apply to the Anchors as well

***

## Checklist for chapter 2.06

**Beginner**

* What is UI scaling?

**Intermediate**

* What is an anchor?
* What is a margin?
* What are the limitations of anchoring (can you anchor to a sibling or a parent)?
* How do you define item dimensions with anchors?

***
