// Muuttaisin tän: Properties and Property Bindings *J: 10-4*
// Noi Perustyypit ei ole se suola

# 3.02 - Properties and Property Bindings

// QML Types and properties 
// Tämähän koskee myös ei visuaalisia tyyppejä
// Ja itse asiassa tarina alkaa QML types...

## QML Types and Properties

QML types are structures in the markup language, and they represent visual and non-visual parts.  
Non-visual QML types include functionality, such as states, transitions, models, paths, gradients and timers. 

// Seuraava kappale voisi alkaa, että Item on sit visual itemien perustyyppi. Vois jopa mainita että C++:ssa QQuickItem
// Sitten jatkaa, että Item ei ole näkyvä kuten alla
// Ei ohjeisteta käyttämään Rectanglea vaan mitä tahansa visuaalista tyyppiä, Rectangle, Image etc. Noita otsikon tyyppejä voisi mainita 
// tässä pelkän Rectanglen sijaan

// Ja site vielä. Tehdään selväski, että yleensä on Window tai sen alityyppi sovelluksen main.qml:n roottina. Omien komponenttien 
// juurena Item on hyvä. Koska wizard nykyään tekee ton Window-olion automaattisesti, niin yritetään olla sotkematta

All visual items in Qt Quick inherit from `Item`, but it's not itself visual. Using `Item` as the top-level QML object (as the root item of your component) will not produce a visual result. If you wish your top-level QML object to produce a visual result, you can use another type such as `Rectangle` or `Image` instead.

Note that even though `Item` is good as a root object for your own components, in the applications `main.qml` you'll usually want to use `Window` or one of its sub-types as the root object. When creating a new QML project with the Qt Creator wizard, the `Window` object is created automatically to `main.qml`.

// Eka lause tästä kappaleesta ennen kuin aletaan puhua Itemistä. Sit vois jatkaa. Although...
// key handling support => key focus handling

Although an `Item` object has no visual appearance, it defines all the attributes that are common across visual items, such as x and y position, width and height, anchoring and key focus handling. It supports layering and is usually used to group visual types. Use the `Item` to create opacity effects, such as when creating an invisible container to hold other components.

// ATTACHED and GROUPED proerpties. Esim. Keys ja anchors { top: jotain; bottom: jotain } *J: Lisäsin Grouped properties selityksen edellisen chapterin Property osaan*

The `Item` type is useful for grouping several items under a single root visual item. For example:

```qml
import QtQuick 2.0

Item {
    id: root
    Text {
        text: "Hello There!"
        color: black
    }
    Image {
        id: imageOne
        x: 80
        width: 100
        height: 100
        source: "tile.png"
    }
    Image {
        id: imageTwo
        x: 190
        width: 100
        height: 100
        fillMode: Image.Tile
        source: "tile.png"
    }
}
```  
// You should bind. MITÄ? Ei ole selitetty bindauksesta vielä mitään. 
// Mikä on : ja = ero
// Ottaisin tähän myös esimerkin Qt.bindingista, että = ja bindingin ero tulee selväksi
// *J: Property bindeistä on nyt oma kappale edellisessä osassa, siellä on myös Qt.binding esimerkki*

Please note that we are only using "magic numbers" in the examples for clarity's sake. In general, you should try to bind x, y, width and height to the parent, or another item, such as the root item of the component.

We discussed properties at the end of the last chapter. You should note that the `id` property is a special property used to identify QML objects. It is used to create relationships between objects. The `id` property of an object allows other objects refer to it in regard to:

* Relative realignment and positioning
* To use its properties
* To change its properties (e.g. for animation)
* For re-use of common types (e.g. gradients, images).

For example:

```qml
Item { 
    width: 300; height: 115 
    Text { 
        id: title 
        x: 50; y: 25 
        text: "Qt Quick" 
        font { family: "Helvetica"; pointSize: parent.width * 0.1 } 
    } 
    
    Rectangle { 
        x: title.x; y: title.y + title.height - height; height: 5 
        width: title.width 
        color: "green" 
    }
} 
```

### Visual Parents and Children

// This section needs to be explained better I feel, it doesn't seem clear

// Tässä vähän toistetaan samaa asiaa. Vika lause on ainakin toistoa, eikö

The `QObject` parent-child relationship was discussed in Part 2. Now we will be taking a look at visual parents and children, as it's important to understand how the concept differs. An item's visual parent may not necessarily be the same as its object parent. The concept of the visual parent in Qt Quick is separate from, but related to, the concept of the object parent within the `QObject` parent hierarchy.

All QML objects have an object parent, which is determined by the object hierarchy in which the object is declared. When working with the `QtQuick` module, the `Item` type is the base type for all visual items provided by this module, and it provides the concept of an additional visual parent, as defined by an item's parent property. Every item has a visual parent; if an item's parent property value is `null`, the item will not be rendered in the scene.

Any object assigned to an item's data property becomes a child of the item within its `QObject` hierarchy, for memory management purposes. Additionally, if an object added to the data property is of the `Item` type, it is also assigned to the `Item::children` property and becomes a child of the item within the visual scene hierarchy. (Most Qt Quick hierarchy crawling algorithms, especially the rendering algorithms, only consider the visual parent hierarchy.)

For convenience, the `Item` data property is its default property. This means that any child item declared within an `Item` object without being assigned to a specific property is automatically assigned to the data property and becomes a child of the item as described above. So, the two code blocks below produce the same result, and you will almost always see the form shown below: 

```qml
import QtQuick 2.0

Item {
    width: 100; height: 100
    
    Rectangle { 
        width: 50;
        height: 50;
        color: "red"
    }
}
```

Rather than the explicit data assignment:

```qml
import QtQuick 2.0

Item {
    width: 100; height: 100

    data: [
        Rectangle {
            width: 50;
            height: 50;
            color: "red"
        }
    ]
}
```

An item's visual parent can be changed at any time by setting its parent property. Thus, an item's visual parent may not necessarily be the same as its object parent.

When an item becomes the child of another item:

* The child's parent refers to its parent item
* The parent's `children` and `childrenRect` properties take that child into account

Declaring an item as a child of another does not automatically mean that the child item will be appropriately positioned or sized to fit within its parent. Some QML types may have in-built behaviors that affect the positioning of child items — for example, a `Row` object automatically re-positions its children into a horizontal formation — but these are behaviors enforced by the types' own specific implementations. Additionally, a parent item will not automatically clip its children to visually contain them within the parent's visual bounds, unless its `clip` property is set to true.

// Binding-harkkaa kehiin jo. Menee liian pitkäksi

// Tässä voisi olla aliotsikko "Some bqsic QML tupes" *J: done*

## Some Basic QML Types

### Rectangle

[Rectangle](http://doc.qt.io/qt-5/qml-qtquick-rectangle.html) items are used to fill areas with solid color or gradients, and/or to provide a rectangular border.

Each `Rectangle` item is painted using either a solid fill color, specified using the color property, or a gradient, defined using a `Gradient` type and set using the gradient property. If both a color and a gradient are specified, the gradient is used.

You can add an optional border to a rectangle with its own color and thickness by setting the `border.color` and `border.width` properties. Set the color to "transparent" to paint a border without a fill color.

You can also create rounded rectangles using the `radius` property. Since this introduces curved edges to the corners of a rectangle, it may be appropriate to set the `Item::antialiasing` property to improve its appearance.

If you need random shapes instead of rectangles, look into the `Shape` type.

// Viittaus Shapesiin, jos haluaa random muotoja 

For example:

```qml
import QtQuick 2.0

Rectangle {
    width: 100
    height: 100
    color: "red"
    border.color: "black"
    border.width: 5
    radius: 10
}
```

### Image

[Image](https://doc.qt.io/qt-5/qml-qtquick-image.html) type displays an image from an URL specified in the `source` property. `Image` type can handle Qt supported URLs and image types (PNG, JPEG, SVG, ...).

The source image can naturally be smaller or bigger than the `Image` item. With the `fillMode` property, you can choose the strategy used when painting the image inside the item.

By default, images are loaded from network resources asynchronously in a separate thread. This way slow networks won't block the UI. Local files are loaded synchronously by default, and this can be overridden by setting the [`asynchronous`](https://doc.qt.io/qt-5/qml-qtquick-image.html#asynchronous-prop) property to `true`. Asynchronous loading is beneficial if there are big files that are not needed be displayed right away in the UI.

To monitor the state of an image, `Image` has [`progress`](https://doc.qt.io/qt-5/qml-qtquick-image.html#progress-prop) and [`status`](https://doc.qt.io/qt-5/qml-qtquick-image.html#status-prop) properties. They can be bound to visualizations in the UI, for example.

When using images with QML, they are often the greatest user of memory in the UI. To minimize memory usage of images that are not part of UI (like user provided resources), they should have their size bounded with the [`sourceSize`](https://doc.qt.io/qt-5/qml-qtquick-image.html#sourceSize-prop) property. `sourceSize` dictates the actual width and height of the loaded image, while the `width` and `height` properties hold the dimensions where the image will be scaled to.

// Pikkuisen modaisin tota esimerkkiä, että se asynkroninen loadaus ja sen tilan nappaaminen tulisi esille. *J: Tilan nappaaminen?*

```qml
import QtQuick 2.0

Image {
   anchors.fill: parent
   asynchronous: true
   source: "bigImage.jpg"
   sourceSize.width: 1024
   sourceSize.height: 1024
}
```

Image data is cached and shared internally, so when using the same `source` the objects will use the same data.

### BorderImage

The [BorderImage](https://doc.qt.io/qt-5/qml-qtquick-borderimage.html) type is used to form a border out of parts of an image by scaling or tiling.

`BorderImage` divides the source image into 9 regions like this:

![BorderImage]({{ "/assets/images/part-2/borderimage2.png" | absolute_url }})

The regions are defined with the `border` property group. Regions formed from the source image are scaled or tiled to create the displayed border image in the following way:

* The corners (regions 1, 3, 7, and 9) are not scaled at all.
* Regions 2 and 8 are scaled according to `horizontalTileMode`.
* Regions 4 and 6 are scaled according to `verticalTileMode`.
* The middle (region 5) is scaled according to both `horizontalTileMode` and `verticalTileMode`.

If the TileMode is set to `Stretch`, the parts of the image are stretched vertically/horizontally if needed. If it's set to `Repeat` the parts of the image are instead repeated. When the width/height of the border regions 2, 4, 6, 8 cannot be repeated in exact multiples of the target width/height, the tilemode [`BorderImage.Round`](https://doc.qt.io/qt-5/qml-qtquick-borderimage.html#horizontalTileMode-prop) can be used to scale the regions to fit in the target.

Example usage:
```qml
BorderImage {
    width: 180; height: 180
    border { left: 30; top: 30; right: 30; bottom: 30 }
    horizontalTileMode: BorderImage.Repeat
    verticalTileMode: BorderImage.Repeat
    source: "pics/borderframe.png"
}
```

### Text

[Text](https://doc.qt.io/qt-5/qml-qtquick-text.html) type can display plain or rich text (using [HTML markup](https://doc.qt.io/qt-5/richtext-html-subset.html)).
To have user editable text you can use [`TextEdit`](https://doc.qt.io/qt-5/qml-qtquick-textedit.html), which is very similar to `Text` type. 

```qml
import QtQuick 2.0

Text {
    text: "Hello <b>World</b>!"
    font.family: "Helvetica"
    font.pointSize: 24
    color: "red"
}
```

// Nyt kun tossa on toi explicitly set alla
// Mikä on explicit ja implicit ero *J: Kun tässä se ero on boolean, explicitly = set, implicit = not set, en tiedä tarviiko sitä selittää*
// Olis hyvä myös mainita, että explicit koko on huono, kun aiheuttaa layout re-calculation *J: done, hopefully in a way that makes sense*

If the `height` and `width` are not explicitly set, `Text` will try to accomodate the dimensions to the text. Generally explicitly setting the size is unoptimal because it causes a layout re-calculcation, so set it if you need to.

To enable text wrapping, [`wrapMode`](https://doc.qt.io/qt-5/qml-qtquick-text.html#wrapMode-prop) needs to be set, otherwise the text will laid on one line.

To customize the font, the properties in group `font` can be changed:
* [`font.family`](https://doc.qt.io/qt-5/qml-qtquick-textedit.html#font.family-prop) choose the family of the font
* [`font.pointSize`](http://doc.qt.io/qt-5/qml-qtquick-text.html#font.pointSize-prop) sets the font size in device independent points
* [`font.pixelSize`](http://doc.qt.io/qt-5/qml-qtquick-text.html#font.pixelSize-prop) sets the font size pixels

To style the text, the property [`style`](http://doc.qt.io/qt-5/qml-qtquick-text.html#style-prop) can be used to change the style to either `Raised`, `Outline` or `Sunken`. Use [`styleColor`](http://doc.qt.io/qt-5/qml-qtquick-text.html#styleColor-prop) to change the color of the added styling

```qml
Row {
    Text { font.pointSize: 24; text: "Normal" }
    Text { font.pointSize: 24; text: "Raised"; style: Text.Raised; styleColor: "#AAAAAA" }
    Text { font.pointSize: 24; text: "Outline";style: Text.Outline; styleColor: "green" }
    Text { font.pointSize: 24; text: "Sunken"; style: Text.Sunken; styleColor: "#AAAAAA" }
}
```

To scale text with other items, you can
* bind the [`font.pixelSize`](http://doc.qt.io/qt-5/qml-qtquick-text.html#font.pixelSize-prop) property to any item geometry
* set the [`fontSizeMode`](http://doc.qt.io/qt-5/qml-qtquick-text.html#fontSizeMode-prop) to scale the text size

```qml
Rectangle { 
    width: 400
    height: 400
    color: "lightblue" 
    
    
    Text { 
        x: parent.width * 0.25
        y: parent.height * 0.25 
        
        text: "Qt Quick"
        font {
            family: "Sans"
            pixelSize: parent.width * 0.1
        } 
    } 
}
```

You can also use [FontMetrics](https://doc.qt.io/qt-5/qml-qtquick-fontmetrics.html) to get the geometry of a certain font:

```qml
Rectangle {

    FontMetrics {
        id: metrics
        font.pointSize: 20
        font.family: "Courier"
    }

    width: 200
    height: metrics.height * 10
    
}
```

## Internationalization

Use the function `qsTr()` to declare translatable strings inside the UI. Qt Quick has extensive internationalization and localization support you can read up on [here](https://doc.qt.io/qt-5/qtquick-internationalization.html)

// Mitä pitää laittaa QML-fileen
// Lyhyesti prosessi, mieluiten kuvan kanssa
// Ja eikö täst saisi harkan.
// Lokalisoi toi login screen

// *J: Lisään tän kunnon osioksi jos siihen päädytään. Kuitenkin: Tästä 3. osastakin on tulossa aika pitkä, enkä oikein näe että tätä*
// *voi selittää kovin lyhyesti. Halutaanko että tästä tulee sellainen 4-6 kappaletta tekstiä + muutama esimerkki?*

***

// Kahteen osaan toi harkka tai sit uusi harkka. Eli binding erikseen ja sit toi PictureFrame
// Sit sais helposti kolmannen. Lokalisoi tekstit. Sitä ei kyllä TMC:llä testata, mutta ainakin pitäisi käydä prosessi läpi
// Ja bindigeissa tosiaan myös se Qt.binding()

### Exercise - PictureFrame

Implement a simple picture frame that will display an image and a caption text in this component.

You can use your own image file if you want!

You will need to add or modify the following items:

Image
- Export the source property of your Image item
- Center the image inside the BorderImage (anchors.centerIn: ...)

BorderImage
- Use the frame.png as the image source, it is approx 85 pixels "wide" in any dimension
- As a frame naturally encloses a picture, declare the width/height to adapt to image size

Text
- Export the text property of 'caption'
- Style the text with an outline style

*****
