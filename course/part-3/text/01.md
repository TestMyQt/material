# 3.01 - Introduction to QML

// Material-Outline 2.11 Global Object: Not included, but explain global object in 3.01 or 3.02?

In Parts 1 and 2 we mainly worked on Qt essentials, which can be found in almost any Qt application, no matter how trivial or complicated the application is. We were discussing about the application logic, the engine implemenating the application behaviour. In addition to logic, however, we usually want to create a user interface to interact with the engine as well. Preferably a flashy, cool looking, fluid user interface that shoots lasers and plays disco music when the user interacts with it. Who doesn't love lasers and disco music?!

In Part 3 we are going to be talking about creating user interfaces using Qt Quick. The programming language we will be using is the declarative scripting language QML, which extends the JavaScript language. Although the application logic can be implemented in JavaScript, the preferred way is to use QML and JavaScript for declaring the UI components, layouts, and animations, while C++ should be used to implement the application logic. This is because performance will almost always be worse with logic implemented using JavaScript in comparison to C++.

We will return to the topic of interconnecting application logic written in C++ with QML in Part 5. In this part, we cover QML language only. 

// Olis hyvä, jos suositeltais Qt Quick Designerin käyttöä

In Qt Creator, there exists a graphical UI design tool, called Qt Quick Designer. The UIs in the course can be composed in Qt Quick Designer, but in many cases a simple text editor is almost as efficient. Qt also provides Qt Design Studio, which is mainly targeted at designers. It allows UI designer to import graphic assets from Adobe Photoshop and further the SW developer to use the UI design, composed in Qt Design Studio. However, for the course exercises Qt Design Studio is little bit too overwhelming. 

## Qt Quick

Qt Quick is a module, consisting of all the basic types and functionality for QML. It includes visual types, interactive types, animations, models and views, particle effects and shader effects. A QML application developer can get access to all of that functionality by importing the corresponding QML modules. The modules are plugins, providing types for the QML engine. A trivial module may be just a folder, consisting of QML text files. 

The Qt Quick QML library is provided by the Qt Quick module. For in-depth information about the various QML types and other functionality provided by Qt Quick, please see the [Qt Quick module](http://doc.qt.io/qt-5/qtquick-index.html) documentation.

When using the Qt Quick module, you will need to know how to write QML applications using the QML language.

Using the Qt Quick module, designers and developers can efficiently work with the same product development project, without long cycles, waiting designers to change or developers implement certain UI features. This can significantly reduce round-trip time between the designer and developer, reducing product time-to-market. 

## QML

QML stands for Qt Meta-Object Language, AKA Qt Modelling Language, and it's a user interface markup language. 

The Qt QML module provides a framework for developing applications and libraries with the QML language. It defines and implements the language and engine infrastructure, and provides an API to enable application developers to extend the QML language with custom types and integrate QML code with JavaScript and C++. The Qt QML module provides both a QML API and a C++ API.

You'll want to work on your application logic using C++. It will in most cases result in better performance than implementing logic using JavaScript. 

![Qt Quick workflow]({{ "/assets/images/part-2/qt_quick_workflow.png" | absolute_url }})

QML is a declarative language that allows user interfaces to be described in terms of their visual components and how they interact and relate with one another. It is a highly readable language that was designed to enable components to be interconnected in a dynamic manner, and it allows components to be easily reused and customized within a user interface. Using the Qt Quick module, designers and developers can easily build fluid animated user interfaces in QML, and have the option of connecting these user interfaces to any back-end C++ libraries.

With QML, UI components are declared with objects, object properties and property bindings, set to define the application behavior. It is possible to declare signals and signal/slot connections similarily to C++. Application behavior can be further scripted through JavaScript, which is a subset of the language. In addition, QML heavily uses Qt, which allows types and other Qt features to be accessible directly from QML applications. In fact, each JavaScript method in a QML type, also called as a component, is a public slot and can be connected to a signal. 

## .qml -files and QML syntax

// Pitäis mainita, että noi attribuutit on niitä QObject propertyjä ja että niitä voi määritellä myös dynaamisesti noissa QML-dokkareissa tai komponenteissa *J: Siirsin lopussa olleen "QML Object Attributes" kappaleen tähän (Alias-osa siitä meni 3.09iin) ja lisäsin siihen juttua Q_Propertystä*

QML is a multi-paradigm language that enables objects to be defined in terms of their attributes and how they relate and respond to changes in other objects. In contrast to purely imperative code, where changes in attributes and behavior are expressed through a series of statements that are processed step by step, QML's declarative syntax integrates attribute and behavioral changes directly into the definitions of individual objects. These attribute definitions can then include imperative code, in the case where complex custom application behavior is needed.

QML source code is generally loaded by the engine through QML (.qml) documents, which are standalone documents of QML code. These can be used to define QML object types that can then be reused throughout an application. A QML document, defining a new type, is also called a component.

Note that type names must begin with an uppercase letter in order to be declared as QML object types in a QML file.

### QML Object Attributes

Every QML object type has a defined set of attributes. Each instance of an object type is created with the set of attributes that have been defined for that object type.

// Viittaus noihin property visibility-sääntöihin olis nyt tarpeen. Katsokaa alla. Lisäsin tuon according... *J: Pitää vielä lisätä tarkemmin visibility säännöt, en ole varma tehdäkö se täällä vai erikseen jossain Item kohdassa jatkossa*

A property is an attribute of an object that can be assigned a static value or bound to a dynamic JavaScript expression or even a code block. A property's value can be read by other objects according to property visibility scope rules. Generally it can also be modified by another object, unless a particular QML type has explicitly disallowed this for a specific property.

// Tähän pitäis saada viimeistään viittaus QObject-propertyihin. *J: Onko alla oleva riittävä vai halutaanko tarkemmin?*

A property may be defined for a type in C++ by registering a `Q_PROPERTY` of a class which is then registered with the QML type system. We discussed these `QObject` properties in the last part. Alternatively, a custom or dynamic property of an object type may be defined in an object declaration in a QML document or component with the following syntax:

```qml
[default] property <propertyType> <propertyName>
```

In this way an object declaration may expose a particular value to outside objects or maintain some internal state more easily.

// Aliakset componenttilukuun, jos mahdollista *J: Siirretty*

// BINDINGS!!!!! Vai onko ne seuraavassa osassa *J: Teen siitä oman osion nyt tähän, siirretään seuraavaan osioon jos ei sovi tänne*

### Grouped Properties

In some cases properties contain a logical group of sub-property attributes. These sub-property attributes can be assigned to using either the dot notation or group notation.

For example, the Text type has a font group property. Below, the first Text object initializes its font values using dot notation, while the second uses group notation:

```qml
Text {
    //dot notation
    font.pixelSize: 12
    font.b: true
}

Text {
    //group notation
    font { pixelSize: 12; b: true }
}
```

### Property Bindings

As we just mentioned, an object's property can be assigned a static value which stays constant until explicitly assigned a new value. However, to make the fullest use of QML and its built-in support for dynamic objecct behaviours, most QML objects use property bindings. Property bindings are a core feature of QML that lets developers specify relationships between different object properties. When property's dependencies change in value, the property is automatically updated according to the specified relationship.

Behind the scenes, the QML engine monitors the property's dependencies (that is, the variables in the binding expression). When a change is detected, the QML engine re-evaluates the binding expression and applies the new result to the property.

To create a property binding, a property is assigned a JavaScript expression that evaluates to the desired value. At its simplest, a binding may be a reference to another property. Take the following example, where the blue Rectangle's height is bound to the height of its parent:

```qml
Rectangle {
    width: 200; height: 200

    Rectangle {
        width: 100
        height: parent.height
        color: "blue"
    }
}
```

Whenever the height of the parent changes, the height of the rectangle automatically updates to the same value.

A binding can contain any valid JavaScript expression or statement. Bindings can access object properties, call methods, and use built-in JavaScript objects such as `Date` or `Math`. A bit more complex example would bind an objects `color` to the length of a text in another object.

```qml
color: myTextInput.text.length <= 10 ? "red" : "blue"
```

Here the object's color changes from blue to red when an other object's (with id `myTextInput`) text becomes longer than 10 characters.

It is important to note that while a bound property's value updates automatically, the binding will be removed if it's later assigned a static value by the JavaScript statement. This is a common source for troubles, especially for beginners! For example:

```qml
import QtQuick 2.0

Rectangle {
    width: 100
    height: width * 2

    focus: true
    Keys.onSpacePressed: {
        height = width * 3
    }
}
```

Here, the Rectangle initially ensures that its height is always twice its width. However, when the space key is pressed, the current value of width*3 will be assigned to height as a static value. After that, the height will remain fixed at this value, even if the width changes. The assignment of the static value removes the binding.

This can be desirable behavious in many cases. If the intention is to give the rectangle a fixed height and stop automatic updates, the code does exactly that. However, if the intention is to establish a new relationship between width and height, then the new binding expression must be wrapped in the `Qt.binding()` function instead:

```qml
import QtQuick 2.0

Rectangle {
    width: 100
    height: width * 2

    focus: true
    Keys.onSpacePressed: {
        height = Qt.binding(function() { return width * 3 })
    }
}
```
Now, after the space key is pressed, the rectangle's height will continue auto-updating to always be three times its width.

### Imports

A QML document may have one or more imports at the top of the file. An import can be any one of:

* a versioned namespace into which types have been registered (e.g., by a plugin)
* a relative directory which contains type-definitions as QML documents
* a JavaScript file

JavaScript file imports must be qualified when imported, so that the properties and methods they provide can be accessed.  

The generic form of the various imports are as follows:

```qml
import Namespace VersionMajor.VersionMinor
import Namespace VersionMajor.VersionMinor as SingletonTypeIdentifier
import "directory"
import "file.js" as ScriptIdentifier
```

Examples:
```qml
import QtQuick 2.0
import QtQuick.LocalStorage 2.0 as Database
import "../privateComponents"
import "somefile.js" as Script
```

### QML Object tree

Syntactically, a block of QML code defines a tree of QML objects to be created. Objects are defined using object declarations that describe the type of object to be created as well as the attributes that are to be given to the object. Any object declaration can define child objects through nested object declarations. In this way, any object declaration implicitly declares an object tree that may contain any number of child objects.

For example, the Rectangle object declaration below includes a Gradient object declaration, which in turn contains two GradientStop declarations:

```qml
import QtQuick 2.0

Rectangle {
    width: 100
    height: 100

    gradient: Gradient {
        GradientStop { position: 0.0; color: "yellow" }
        GradientStop { position: 1.0; color: "green" }
    }
}
```

Note, however, that this is a parent-child relationship in the context of the QML object tree, not in the context of the visual scene. The concept of a parent-child relationship in a visual scene is provided by the `Item` type from the QtQuick module, which is the base type for most QML types, as most QML objects are intended to be visually rendered. We we will discuss parent-child relationship in visual scene in the next chapter. 
{: .note}

## Including QML in your projects

To include QML in your projects, you need to complete these three steps:  

1. To include the definitions of the module's classes, use the following directive:

    ```cpp
    #include <QtQml>
    ```

2. The QML types in Qt QML are available through the QtQML import. To use the types, add the following import statement to your .qml file:

    ```qml
    import QtQml 2.0
    ```

3. To link against the module, add this line to your qmake .pro file:

    ```qmake
    QT += qml
    ```

## Prototyping with Quick UI projects and qmlscene

// This needs to be updated if `qmlscene` is depracated?

To prototype user interfaces even more quickly, one can create a [Qt Quick UI](http://doc.qt.io/qtcreator/quick-projects.html) project. These projects do not contain any C++ code, resource `.qrc` or deployment code (`qmake` files). This way the potential designer can launch the application without compiling any code or developer can quickly share proof-of-concepts. For example in Qt Design Studio, it is possible to create Qt Quick UI projects only. The QML Scene can be executed also remotely in a mobile or embedded device and any change in UI editor will be reflected in the UI immediately. Qt Quick Designer does not have similar feature. 

One can also enable a keyboard shortcut for the prototyping application [`qmlscene`](https://doc.qt.io/qt-5/qtquick-qmlscene.html), which renders any arbitary `.qml` files. `qmlscene` can be enabled in Qt Creator by going to `Options -> Environment -> Keyboard -> search for qmlscene -> assign shortcut`. When a `.qml` file is open in the Edit or Design mode, hit the shortcut to render the file.

*****
